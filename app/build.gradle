buildscript {
    ext {
        support_lib_version = '26.1.0'
        firebase_version = '11.4.2'
    }

    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        // These docs use an open ended version so that our plugin
        // can be updated quickly in response to Android tooling updates

        // We recommend changing it to the latest version from our changelog:
        // https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
        classpath 'io.fabric.tools:gradle:1.24.4'
        classpath 'com.cookpad.android.licensetools:license-tools-plugin:0.21.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'
apply plugin: 'com.cookpad.android.licensetools'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.bl_lia.kirakiratter"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode Integer.parseInt(System.getenv("BITRISE_BUILD_NUMBER") != null ? System.getenv("BITRISE_BUILD_NUMBER") : "1")
        versionName "0.5.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            keyAlias System.getenv("BITRISEIO_ANDROID_KEYSTORE_ALIAS")
            keyPassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PRIVATE_KEY_PASSWORD")
            storeFile file(System.getenv("HOME") + "/keystores/release.jks")
            storePassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PASSWORD")
        }
    }
    buildTypes {
        all {
            buildConfigField "String", "API_URL", "\"https://kirakiratter.com\""
            buildConfigField "String", "GOOGLE_TRANSLATE_API_URL", "\"https://translation.googleapis.com\""
        }
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".debug"
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            lintOptions {
                disable 'MissingTranslation'
            }
            signingConfig signingConfigs.release
            ext.betaDistributionGroupAliases = 'testers'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/LICENSE'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    kapt group: 'com.google.dagger', name: 'dagger-compiler', version: '2.10'
    kapt "com.github.hotchemi:permissionsdispatcher-processor:2.4.0"
    implementation 'com.android.support:multidex:1.0.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$support_lib_version"
    implementation "com.android.support:recyclerview-v7:$support_lib_version"
    implementation "com.android.support:design:$support_lib_version"
    implementation 'com.android.support.constraint:constraint-layout:1.0.1'
    implementation "com.android.support:percent:$support_lib_version"
    implementation 'com.google.android:flexbox:0.2.6'
    implementation group: 'com.google.dagger', name: 'dagger', version: '2.10'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.6.0'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp-urlconnection', version: '3.6.0'
    implementation group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: '3.6.0'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    implementation group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.1.6'
    implementation group: 'io.reactivex.rxjava2', name: 'rxandroid', version: '2.0.1'
    implementation group: 'com.squareup.retrofit2', name: 'retrofit', version: '2.2.0'
    implementation group: 'com.squareup.retrofit2', name: 'converter-gson', version: '2.2.0'
    implementation group: 'com.squareup.retrofit2', name: 'adapter-rxjava2', version: '2.2.0'
    implementation group: 'com.trello.rxlifecycle2', name: 'rxlifecycle', version: '2.0.1'
    implementation group: 'com.trello.rxlifecycle2', name: 'rxlifecycle-android', version: '2.0.1'
    implementation group: 'com.trello.rxlifecycle2', name: 'rxlifecycle-kotlin', version: '2.0.1'
    implementation group: 'com.trello.rxlifecycle2', name: 'rxlifecycle-components', version: '2.0.1'
    implementation group: 'com.squareup.picasso', name: 'picasso', version: '2.5.2'
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.10.2'
    implementation group: 'commons-io', name: 'commons-io', version: '2.5'
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') {
        transitive = true;
    }
    implementation 'jp.wasabeef:picasso-transformations:2.1.2'
    implementation "com.google.firebase:firebase-core:$firebase_version"
    implementation "com.google.firebase:firebase-config:$firebase_version"
    implementation "com.google.firebase:firebase-messaging:$firebase_version"
    implementation "com.google.firebase:firebase-perf:$firebase_version"
    implementation 'com.firebase:firebase-jobdispatcher:0.6.0'
    implementation 'net.danlew:android.joda:2.9.9'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'
    implementation 'com.github.chrisbanes:PhotoView:2.0.0'
    implementation("com.github.hotchemi:permissionsdispatcher:2.4.0") {
        exclude module: "support-v13"
    }

    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
}
repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}

apply plugin: 'com.google.gms.google-services'

